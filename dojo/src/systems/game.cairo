use starknet::{ContractAddress};
use dojo::world::IWorldDispatcher;
use feral::libs::gameplay::{
    Direction,
    GameState,
};


#[starknet::interface]
pub trait IGame<TState> {
    // IWorldProvider
    fn world_dispatcher(self: @TState) -> IWorldDispatcher;

    //-----------------------------------
    // IERC721ComboABI start
    //
    // (ISRC5)
    fn supports_interface(self: @TState, interface_id: felt252) -> bool;
    // (IERC721)
    fn balance_of(self: @TState, account: ContractAddress) -> u256;
    fn owner_of(self: @TState, token_id: u256) -> ContractAddress;
    fn safe_transfer_from(ref self: TState, from: ContractAddress, to: ContractAddress, token_id: u256, data: Span<felt252>);
    fn transfer_from(ref self: TState, from: ContractAddress, to: ContractAddress, token_id: u256);
    fn approve(ref self: TState, to: ContractAddress, token_id: u256);
    fn set_approval_for_all(ref self: TState, operator: ContractAddress, approved: bool);
    fn get_approved(self: @TState, token_id: u256) -> ContractAddress;
    fn is_approved_for_all(self: @TState, owner: ContractAddress, operator: ContractAddress) -> bool;
    // (IERC721Metadata)
    fn name(self: @TState) -> ByteArray;
    fn symbol(self: @TState) -> ByteArray;
    fn token_uri(self: @TState, token_id: u256) -> ByteArray;
    fn tokenURI(self: @TState, tokenId: u256) -> ByteArray;
    //-----------------------------------
    // IERC721Minter
    fn max_supply(self: @TState) -> u256;
    fn reserved_supply(self: @TState) -> u256;
    fn available_supply(self: @TState) -> u256;
    fn minted_supply(self: @TState) -> u256;
    fn total_supply(self: @TState) -> u256;
    fn last_token_id(self: @TState) -> u256;
    fn is_minting_paused(self: @TState) -> bool;
    fn is_minted_out(self: @TState) -> bool;
    fn is_owner_of(self: @TState, address: ContractAddress, token_id: u256) -> bool;
    fn token_exists(self: @TState, token_id: u256) -> bool;
    //-----------------------------------
    // IERC7572ContractMetadata
    fn contract_uri(self: @TState) -> ByteArray;
    fn contractURI(self: @TState) -> ByteArray;
    //-----------------------------------
    // IERC4906MetadataUpdate
    //-----------------------------------
    // IERC2981RoyaltyInfo
    fn royalty_info(self: @TState, token_id: u256, sale_price: u256) -> (ContractAddress, u256);
    fn default_royalty(self: @TState) -> (ContractAddress, u128, u128);
    fn token_royalty(self: @TState, token_id: u256) -> (ContractAddress, u128, u128);
    // IERC721ComboABI end
    //-----------------------------------

    // game
    fn mint_game(ref self: TState, recipient: ContractAddress) -> u128;
    fn submit_game(ref self: TState, game_id: u128, moves: Array<Direction>) -> GameState;
    fn start_game(self: @TState, game_id: u128) -> GameState;
    fn move(self: @TState, game_state: GameState, direction: Direction) -> GameState;
    // admin
    fn set_minting_paused(ref self: TState, is_paused: bool);
    fn update_token_metadata(ref self: TState, token_id: u256);
    fn update_tokens_metadata(ref self: TState, from_token_id: u256, to_token_id: u256);
    fn update_contract_metadata(ref self: TState);
}

#[starknet::interface]
pub trait IGamePublic<TState> {
    fn mint_game(ref self: TState, recipient: ContractAddress) -> u128;
    fn submit_game(ref self: TState, game_id: u128, moves: Array<Direction>) -> GameState;
    fn start_game(self: @TState, game_id: u128) -> GameState;
    fn move(self: @TState, game_state: GameState, direction: Direction) -> GameState;
    // admin
    fn set_minting_paused(ref self: TState, is_paused: bool);
    fn update_token_metadata(ref self: TState, token_id: u256);
    fn update_tokens_metadata(ref self: TState, from_token_id: u256, to_token_id: u256);
    fn update_contract_metadata(ref self: TState);
    // fn create_trophies(ref self: TState);
    // fn burn(ref self: TState, token_id: u256);
}

#[dojo::contract]
pub mod game {
    use starknet::ContractAddress;
    use dojo::{
        world::{WorldStorage, IWorldDispatcherTrait},
        model::{ModelStorage},
        // event::{EventStorage},
    };

    //-----------------------------------
    // ERC721 start
    //
    use openzeppelin_introspection::src5::SRC5Component;
    use openzeppelin_token::erc721::ERC721Component;
    use nft_combo::erc721::erc721_combo::ERC721ComboComponent;
    use nft_combo::erc721::erc721_combo::ERC721ComboComponent::{ERC721HooksImpl};
    use nft_combo::utils::renderer::{ContractMetadata, TokenMetadata};
    // use achievement::components::achievable::AchievableComponent;
    component!(path: SRC5Component, storage: src5, event: SRC5Event);
    component!(path: ERC721Component, storage: erc721, event: ERC721Event);
    component!(path: ERC721ComboComponent, storage: erc721_combo, event: ERC721ComboEvent);
    // component!(path: AchievableComponent, storage: achievable, event: AchievableEvent);
    impl ERC721InternalImpl = ERC721Component::InternalImpl<ContractState>;
    impl ERC721ComboInternalImpl = ERC721ComboComponent::InternalImpl<ContractState>;
    #[abi(embed_v0)]
    impl ERC721ComboMixinImpl = ERC721ComboComponent::ERC721ComboMixinImpl<ContractState>;
    // impl AchievableInternalImpl = AchievableComponent::InternalImpl<ContractState>;
    #[storage]
    struct Storage {
        #[substorage(v0)]
        src5: SRC5Component::Storage,
        #[substorage(v0)]
        erc721: ERC721Component::Storage,
        #[substorage(v0)]
        erc721_combo: ERC721ComboComponent::Storage,
        // #[substorage(v0)]
        // achievable: AchievableComponent::Storage,
    }
    #[event]
    #[derive(Drop, starknet::Event)]
    enum Event {
        #[flat]
        SRC5Event: SRC5Component::Event,
        #[flat]
        ERC721Event: ERC721Component::Event,
        #[flat]
        ERC721ComboEvent: ERC721ComboComponent::Event,
        // #[flat]
        // AchievableEvent: AchievableComponent::Event,
    }
    //
    // ERC721 end
    //-----------------------------------

    use feral::models::{
        game_info::{GameInfo},
    };
    use feral::libs::{
        metadata,
        hash::{make_seed},
        dns::{SELECTORS},
        gameplay::{
            GameState,
            GameplayTrait,
            Direction,
        },
    };
    use nft_combo::utils::renderer::{Attribute};

    pub mod Errors {
        pub const INVALID_CALLER: felt252       = 'FERAL: Invalid caller';
        pub const INVALID_GAME: felt252         = 'FERAL: Invalid game';
        pub const INVALID_TILE: felt252         = 'FERAL: Invalid tile';
        pub const INVALID_BEAST: felt252        = 'FERAL: Invalid beast';
        pub const GAME_FINISHED: felt252        = 'FERAL: Game finished';
        pub const INVALID_DIRECTION: felt252    = 'FERAL: Invalid direction';
    }

    fn dojo_init(ref self: ContractState) {
        // initialize ERC721
        self.erc721_combo.initializer(
            metadata::TOKEN_NAME(),
            metadata::TOKEN_SYMBOL(),
            Option::None, // use hooks
            Option::None, // use hooks
            Option::None, // infinite supply
        );

        // create trophies/achievements
        // let mut world: WorldStorage = self.world_default();
        // self._create_trophies(ref world);
    }

    #[generate_trait]
    impl WorldDefaultImpl of WorldDefaultTrait {
        #[inline(always)]
        fn world_default(self: @ContractState) -> WorldStorage {
            (self.world(@"feral"))
        }
    }


    //-----------------------------------
    // IGamePublic
    //
    #[abi(embed_v0)]
    impl GameTokenPublicImpl of super::IGamePublic<ContractState> {

        //-----------------------------------
        // minting
        //
        fn mint_game(ref self: ContractState, recipient: ContractAddress) -> u128 {
            let mut world: WorldStorage = self.world_default();

            // mint
            let token_id: u128 = self.erc721_combo._mint_next(recipient).low;

            // generate seed
            let contract_address: ContractAddress = starknet::get_contract_address();
            let seed: felt252 = make_seed(contract_address, token_id);

            // save token
            world.write_model(@GameInfo {
                game_id: token_id,
                minter_address: recipient,
                seed,
            });

            (token_id)
        }

        // fn burn(ref self: ContractState, token_id: u256) {
        //     let owner: ContractAddress = self.owner_of(token_id);
        //     self.erc721_combo._burn(token_id);
        // }

        //-----------------------------------
        // gameplay
        //

        // off-chain play
        fn start_game(self: @ContractState, game_id: u128) -> GameState {
            let world: WorldStorage = self.world_default();
            assert(self.token_exists(game_id.into()), Errors::INVALID_GAME);
            // create new game state
            let mut game_state: GameState = world.start_game(game_id);
            // calculate score
            // game_state.calc_score(); // start with zero!
            (game_state)
        }

        fn move(self: @ContractState, mut game_state: GameState, direction: Direction) -> GameState {
            assert(self.token_exists(game_state.game_id.into()), Errors::INVALID_GAME);
            // create new game state
            game_state.move(direction);
            // calculate score
            game_state.calc_score();
            (game_state)
        }

        fn submit_game(ref self: ContractState, game_id: u128, moves: Array<Direction>) -> GameState {
            let world: WorldStorage = self.world_default();
            assert(self.token_exists(game_id.into()), Errors::INVALID_GAME);
            // make a new game
            let mut game_state: GameState = world.start_game(game_id);
            // process all moves
            for i in 0..moves.len() {
                let direction: Direction = *moves[i];
                game_state.move(direction);
                // early exit if the game is finished
                if (game_state.finished) {
                    break;
                }
            }
            // calculate score
            game_state.calc_score();
            // TODO: leaderboards
            // TODO: transfer ownership to top player
            // returns the final state
            (game_state)
        }


        //-----------------------------------
        // admin
        //
        fn set_minting_paused(ref self: ContractState, is_paused: bool) {
            let world: WorldStorage = self.world_default();
            self._assert_caller_is_owner(@world);
            self.erc721_combo._set_minting_paused(is_paused);
        }
        fn update_token_metadata(ref self: ContractState, token_id: u256) {
            // let mut world: WorldStorage = self.world_default();
            // self._assert_caller_is_owner(@world);
            self.erc721_combo._emit_metadata_update(token_id);
        }
        fn update_tokens_metadata(ref self: ContractState, from_token_id: u256, to_token_id: u256) {
            let world: WorldStorage = self.world_default();
            self._assert_caller_is_owner(@world);
            self.erc721_combo._emit_batch_metadata_update(from_token_id, to_token_id);
        }
        fn update_contract_metadata(ref self: ContractState) {
            let world: WorldStorage = self.world_default();
            self._assert_caller_is_owner(@world);
            self.erc721_combo._emit_contract_uri_updated();
        }
        // fn create_trophies(ref self: ContractState) {
        //     let mut world: WorldStorage = self.world_default();
        //     self._assert_caller_is_owner(@world);
        //     self._create_trophies(ref world);
        // }
    }


    //-----------------------------------
    // Internal
    //
    #[generate_trait]
    impl InternalImpl of InternalTrait {
        #[inline(always)]
        fn _assert_caller_is_owner(self: @ContractState, world: @WorldStorage) {
            assert(self._caller_is_owner(world), Errors::INVALID_CALLER);
        }
        fn _caller_is_owner(self: @ContractState, world: @WorldStorage) -> bool {
            ((*world.dispatcher).is_owner(SELECTORS::GAME, starknet::get_caller_address()))
        }
        
        // fn _create_trophies(ref self: ContractState, ref world: WorldStorage) {
        //     let mut trophy_id: u8 = 1;
        //     while (trophy_id <= TROPHIES::COUNT) {
        //         let trophy: Trophy = trophy_id.into();
        //         self.achievable.create(
        //             world,
        //             id: trophy.identifier(),
        //             hidden: trophy.hidden(),
        //             index: trophy.index(),
        //             points: trophy.points(),
        //             start: trophy.start(),
        //             end: trophy.end(),
        //             group: trophy.group(),
        //             icon: trophy.icon(),
        //             title: trophy.title(),
        //             description: trophy.description(),
        //             tasks: trophy.tasks(),
        //             data: trophy.data(),
        //         );
        //         trophy_id += 1;
        //     }
        // }
    }


    //-----------------------------------
    // ERC721ComboHooksTrait
    //
    pub impl ERC721ComboHooksImpl of ERC721ComboComponent::ERC721ComboHooksTrait<ContractState> {
        fn render_contract_uri(self: @ERC721ComboComponent::ComponentState<ContractState>) -> Option<ContractMetadata> {
            // https://docs.opensea.io/docs/contract-level-metadata
            let metadata: ContractMetadata = ContractMetadata {
                name: self.name(),
                symbol: self.symbol(),
                description: metadata::DESCRIPTION(),
                image: Option::Some(metadata::CONTRACT_IMAGE()),
                banner_image: Option::Some(metadata::BANNER_IMAGE()),
                featured_image: Option::None,
                external_link: Option::Some(metadata::EXTERNAL_LINK()),
                collaborators: Option::None,
            };
            (Option::Some(metadata))
        }

        fn render_token_uri(self: @ERC721ComboComponent::ComponentState<ContractState>, token_id: u256) -> Option<TokenMetadata> {
            let self: @ContractState = self.get_contract(); // get the component's contract state
            let mut world: WorldStorage = self.world_default();
            // attributes and metadata
            let game_id: u128 = token_id.low;
            let token_info: GameInfo = world.read_model(game_id);
            let mut attributes: Span<Attribute> = array![
                // Attribute {
                //     key: "Act",
                //     value: format!("{}", token_info.act_number),
                // },
            ].span();
            let mut additional_metadata: Span<Attribute> = array![
                Attribute {
                    key: "Seed",
                    value: format!("0x{:x}", token_info.seed),
                },
            ].span();
            // https://docs.opensea.io/docs/metadata-standards#metadata-structure
            let metadata: TokenMetadata = TokenMetadata {
                token_id,
                name: format!("{} #{}", metadata::TOKEN_NAME(), game_id),
                description: metadata::DESCRIPTION(),
                image: Option::Some(metadata::CONTRACT_IMAGE()),
                image_data: Option::None,
                external_url: Option::Some(metadata::EXTERNAL_LINK()), // TODO: format external token link
                background_color: Option::Some(metadata::BACKGROUND_COLOR()),
                animation_url: Option::None,
                youtube_url: Option::None,
                attributes: Option::Some(attributes),
                additional_metadata: Option::Some(additional_metadata),
            };
            (Option::Some(metadata))
        }
    }

}
